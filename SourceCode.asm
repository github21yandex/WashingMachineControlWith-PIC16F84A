;====================================================================
; Main.asm file generated by New Project wizard
;
; Created:   Sat Sep 11 2021
; Processor: PIC16F84A
; Compiler:  MPASM (Proteus)
;====================================================================

;====================================================================
; DEFINITIONS
;====================================================================

#include p16f84a.inc                ; Include register definition file

;====================================================================
; VARIABLES
;====================================================================
	    PROGRAM_NO EQU 0X0C;				A3  A4	-	PIC'in A3 VE A4 GIRISLERINE GÖRE YIKAMA PROGRAMI BU ADRESE KAYDEDILIR;
							 ;			1	1	-	NORMAL
							 ;			0	1	-	ÇOK KIRLI
							 ;			1	0	-	HIZLI YIKAMA
							 ;			0	0	-	EKONOMIK
							 
	    DURUM_NO	EQU 0X0D;			X	B1  B2	-	PIC'in B1 VE B2 ÇIKISLARINA GÖRE MAKINA DURUMU BU ADRESE KAYDEDILIR;
							 ;		0	0	0	-	YIKAMA
							 ;		0	0	1	-	SIKMA
							 ;		0	1	0	-	DURAKLAMA
							 ;		0	1	1	-	BITTI
							
							 
	    MAX_SU		EQU 0X0E;
	    MAX_SICAKLIK	EQU 0X0F;
	    
	   
	    
	    NORMAL_YIKAMA_ADIM1_SAYKIL_SAYISI EQU 0X10;
	    NORMAL_YIKAMA_ADIM2_SAYKIL_SAYISI EQU 0X11;
	    NORMAL_YIKAMA_ADIM3_SAYKIL_SAYISI EQU 0X12;
	    
	   
	    COUNT_1 		EQU 0X20;
	    COUNT_2		EQU 0X21;
	    COUNT_3		EQU 0X22;
	    COUNT_4		EQU 0X23;
	    SAG_SOL_FLAG	EQU 0X24; 1= SAG     0=SOL
	    
;PWM ILE ILGILI DEGISKENLER
	    CYCLE				EQU 0X30;  BIR SAYKILIN TOPLAM NÜMERIK DEGERI. (BU UYGULAMADA 250)
	    PON					EQU 0X31; BIR SAYKILDA PWM ON ZAMANI NÜMERIK DEGERI (BU UYGULAMADA 50)
	    POFF					EQU 0X32; BIR SAYKILDA PWM OFF ZAMANI NÜMERIK DEGERI (BU UYGULAMADA 200 KISACA POFF = CYCLE - PON
									  ; YANI BIR SAYKIL 250 BIRIMDIR VE BUNUN 50 SINDE VOLTAJ 5V 200 ÜNDE 0V ÇIKIS ALINARAK KARA DALGA ELDE EDILECEKTIR.

	    PWM_COUNT1	EQU 0X33;
	    PWM_COUNT2	EQU 0X34;
	    PWM_COUNT3	EQU 0X35;
	    PWM_COUNT4	EQU 0X36;
	    
	    SAGA_SOLA_DONME_SURESI		EQU 0X40;
	    SIKMA_SURESI					EQU 0X41;
	    BOSTA_BEKLEME_SURESI		EQU 0X42;
	    
;INTERRUPT ILE ILGILI DEGISKENLER
	    DURUM_NO_TEMP				EQU 0X4A;
	    W_TEMP          					EQU 0X4B;
	    STATUS_TEMP 					EQU 0X4C;
	    COUNT_INT      	 				EQU 0X4D;
	    PORTA_STATUS 				EQU 0X4E;
	    PORTB_STATUS				EQU 0X4F;
;====================================================================
; RESET and INTERRUPT VECTORS
;====================================================================

      ; Reset Vector
RST   code  0x00 

	 ;ORG 0X00;
	 GOTO MAIN
	 
	 ORG 0x04;		INTERRUPT DURUMUNDA PROGRAMIN BASLAYACAGI ADRESI BELIRTIR.
	 GOTO ISR;		INTERRUPT GERÇEKLESTIGINDE PROGRAM ISR ETIKETINE DALLANIR.
	 
	 

;====================================================================
; CODE SEGMENT
;====================================================================

PGM   code

MAIN
	    CLRF PORTA;
	    CLRF PORTB;
	    
	    MOVLW D'25';
	    MOVWF SAGA_SOLA_DONME_SURESI;	25*25*7.5/1000=4.69 SANIYE
	    
	    MOVLW D'36';
	    MOVWF SIKMA_SURESI;					36*36*7.5/1000=9.72 SANIYE
	    
	    MOVLW D'5';							DEGER ILE DOGRU ORANTILI, 5 SANIYE BOSTA BEKLER
	    MOVWF BOSTA_BEKLEME_SURESI;
	    
	    MOVLW D'1';
	    MOVWF NORMAL_YIKAMA_ADIM1_SAYKIL_SAYISI;
	    
	    MOVLW D'1';
	    MOVWF NORMAL_YIKAMA_ADIM2_SAYKIL_SAYISI;
	    
	    MOVLW D'1';
	    MOVWF NORMAL_YIKAMA_ADIM3_SAYKIL_SAYISI;

	    ; Write your code here
	    
	    
	 
	    BSF STATUS, 5;		-BANK-1'E GEÇIS
	    
	    BSF TRISA, 0;	IN		-MAKINA ON/OFF TUSU
	    BCF TRISA, 1;	OUT	-SU TAHLIYE MOTORU ON/OFF
	    BCF TRISA, 2;	OUT	-HEATER ON/OFF SINYALI
	    BSF TRISA, 3;	IN		- PROGRAM SECIM GIRISI
	    BSF TRISA, 4;	IN		-PROGRAM SECIM GIRISI
	 
      
	    BSF TRISB, 0;	IN		-BASLA/DURAKLAMA
	    BCF TRISB, 1;	OUT	-LED ICIN 74HC138 A PORT GIRISI
	    BCF TRISB, 2;	OUT	-LED ICIN 74HC138 B PORT GIRISI
	    BCF TRISB, 3;	OUT	-SU GIRIS VANASI SELENOIDI ON/OFF
	    BSF TRISB, 4;	IN		-MAX SU SEVIYESI BILDIRIM GIRISI
	    BCF TRISB, 5;	OUT	-KAZAN MOTORU SAGA YAVAS/HIZLI CEVIRME 
	    BCF TRISB, 6;	OUT	-KAZAN MOTORU SOLA YAVAS/HIZLI CEVIRME
	    BSF TRISB, 7;	IN		-AYARLANAN SICAKLIGA ULASILDI BILDIRIMI
	    
	    MOVLW B'11010111'	;0,1,2 = PRESCALER
							;3 = PRESCALER AYIRMA BITI                  0-TMR0           1-WDT
							;4 = 1-RA4/TOCKI YÜKSELEN KENAR
							;5 = 1-HARICI KOMUT SAYKILI RA4/TOCKI                       0-INT DAHILI KOMUT SAYKILI SEÇIMI
							;6 = HARICI KESME IÇIN KENAR SEÇME BITI   0-YÜKSELEN KENAR 1-ALÇALAN KENAR
							;7 = 1- PORTB UCUNDAKI PULL-UPLAR GEÇERSIZ OLUR 0-PORTB UCUNDAKI PULL-UPLAR GEÇERLI OLUR

	    MOVWF OPTION_REG;
	   
	 
	    BCF STATUS, 5;	BANK-0'A GEÇIS
	
LOOP_MAIN

	    BCF INTCON, GIE;					;GENEL INTERRUPT DISABLE YAPILDI

	    BSF PORTA, 1;						SU TAHLIYE MOTORU OFF
	    
	    BCF PORTA, 2;						HEATER OFF
	    
	    BCF PORTB, 3;						SU VANASI OFF
	    
	    BSF PORTB, 5;						KAZAN MOTORU SAGA DÖNÜS OFF
	    
	    BSF PORTB, 6;						KAZAN MOTORU SOLA DÖNÜS OFF
	    
	    
	    
A0_BEKLEME
	    BTFSS PORTA, 0;
	    GOTO A0_BEKLEME;
	    
B0_BEKLEME

	    BTFSS PORTB, 0;
	    GOTO B0_BEKLEME;
	    
   
	    	    
	    ;PIC'IN A3 VE A4 GIRISLERINDEKI SINYALE GÖRE PROGRAM NUMARASI PROGRAM_NO REGISTER'INA KAYDEDILIR;	    

	    CLRF PROGRAM_NO;
	    	    	    
	    BTFSC PORTA, 3;
	    BSF PROGRAM_NO, 0;
	    
	    BTFSC PORTA, 4;
	    BSF PROGRAM_NO, 1;
	    
    

	    ;MAKINA DURUMUNUN KAYDEDILDIGI DURUM_NO REGISTER'I ILKLENDIRIRLIR VE LEDLER YAKILIR

	    CLRF DURUM_NO;					YIKAMA OLARAK KAYDEDILDI.
	    CALL LEDLERI_YAK;
	    
	    
	    ;ISTENEN INTERRUPTLAR AKTIF HALE GETIRILIR
	    
	    CLRF INTCON;
	    
	    BSF INTCON, T0IE;					ZAMAN ASIMI INTERRUPTI AKTIFLESTIRILIDI.
	    BSF INTCON, INTE;					RB0 HARICI INTERRUPT'I AKTIFLESTIRILDI.
	    BSF INTCON, RBIE;					RB PORTLARI(RB4-RB7 ARASI) DEGISIMDE INTERRUPT AKTIF HALE GETIRILDI
	    
	    BSF INTCON, GIE;					GLOBAL INTERRUP BIT'I AKTIF EDILDI..
	    
	    
  
;==============================================================================
;SEÇILEN YIKAMA PROGRAMI NUMARASINA GÖRE ILGILI ALT PROGRAM ÇAGRILIR
;==============================================================================
	    
	    MOVLW B'00';					
	    SUBWF PROGRAM_NO, W;
	    BTFSC STATUS, Z;
	    GOTO EKONOMIK_YIKAMA

	    MOVLW B'01';
	    SUBWF PROGRAM_NO, W;
	    BTFSC STATUS, Z;
	    GOTO HIZLI_YIKAMA
	    
	    MOVLW B'10';
	    SUBWF PROGRAM_NO, W;
	    BTFSC STATUS, Z;
	    GOTO COK_KIRLI_YIKAMA;
	    
	    MOVLW B'11';
	    SUBWF PROGRAM_NO, W;
	    BTFSC STATUS, Z;
	    GOTO NORMAL_YIKAMA;
	    

EKONOMIK_YIKAMA
	    CALL EKONOMIK_PROG
	    GOTO PROGRAM_SONU

HIZLI_YIKAMA
	    CALL HIZLI_PRO;
	    GOTO PROGRAM_SONU
	    
COK_KIRLI_YIKAMA
	    CALL COK_KIRLI_PROGRAM
	    GOTO PROGRAM_SONU

NORMAL_YIKAMA
	    CALL NORMAL_PROG
	    GOTO PROGRAM_SONU



PROGRAM_SONU
	    MOVLW B'11';
	    MOVWF DURUM_NO;				DURUM NO B'11' BITTI OLARAK KAYDEDILIR
	    CALL LEDLERI_YAK;
	    
BITTI
	    BTFSC PORTA, 0;				BURADA A0 GIRISI CLEAR OLANA KADAR SONSUZ DÖNGÜDE BEKLER
	    GOTO BITTI;
	    CLRF DURUM_NO;
	    GOTO MAIN;
	
	
	    
;===========================================================================
;INTERRUPT PROGRAMI
;===========================================================================

ISR										;B0 YA DA B4,B5,B6,B7 GIRISLERINDEKI DEGISME SONUCU INTERRUPT PROGRAMI BURADAN ITIBAREN BASLAR
	    
	   
	    
	    
	    
PUSH
	    MOVWF W_TEMP;
	    
	    SWAPF STATUS, W; 				STATUS REGISTER'ININ Z BAYRAGI SWAPF KOMUTUNDAN ETKILENMEDIGI IÇIN SWAPF KULLANILDI. MOVF DE KULLANILABILIRDI ANCAK O ZAMAN STATUS REGISTER'INIR Z FLAG'I ETKILENEBILIRDI. 
	    MOVWF STATUS_TEMP;
	    
	    MOVF DURUM_NO, W;
	    MOVWF DURUM_NO_TEMP;
	    
	    ;MOVF PORTA, W;
	    ;MOVWF PORTA_STATUS;
	    
	    ;MOVF PORTB, W;
	    ;MOVWF PORTB_STATUS;
;===========================================
;INTERRUPT PROGRAMI BURADAN ITIBAREN YAZILIR
;============================================
	    
	    INCF COUNT_INT;								;GERÇEKLESEN TOPLAM INTERRUPT SAYISINI TUTAR
	    

DURAKLAMA

	    BTFSS INTCON, INTF;
	    GOTO DURAKLAMA_SONU

	    MOVLW B'01';
	    MOVWF DURUM_NO;
	    CALL LEDLERI_YAK;
	    
	    
	    
	    BSF PORTA, 1;		SIKMA MOTORU OFF.
	    BCF PORTA, 2;		HEATER OFF
	    BCF PORTB, 3;		SU VANASI OFF
	    BSF PORTB, 5;		KAZAN MOTORU SAGA DÖNÜS OFF
	    BSF PORTB, 6;		KAZAN MOTORU SOLA DÖNÜS OFF
	    

LOOP_DURAKLAMA
	    BTFSS PORTB, 0;
	    GOTO LOOP_DURAKLAMA
	    
DURAKLAMA_SONU


DURUM_NO_KONTROL

	    MOVF DURUM_NO, W;
	    SUBLW B'00';
	    BTFSS STATUS, Z;
	    GOTO VANA_VE_HEATERI_KAPAT;
	    GOTO DURUM_NO_KONTROL_SONU;

      VANA_VE_HEATERI_KAPAT
	    BCF PORTA, 2;
	    BCF PORTB, 3;
	    GOTO INTERRUPT_SONU;
	    
DURUM_NO_KONTROL_SONU

	    
	    	
SU_SEVIYESI_KONTROL
	    
	    BTFSS PORTB, 4;
	    GOTO VANA_AC;
	    GOTO VANA_KAPAT;
	  
      VANA_AC
	    BSF PORTB, 3;
	    GOTO SU_SEVIYESI_KONTROL_SON  

      VANA_KAPAT
	    BCF PORTB, 3;
	    GOTO SU_SEVIYESI_KONTROL_SON
	    
SU_SEVIYESI_KONTROL_SON



	    
SICAKLIK_KONTROL

	    BTFSS PORTB, 4;
	    GOTO HEATER_OFF
	    
	    BTFSC PORTB, 7;
	    GOTO HEATER_OFF
	    GOTO HEATER_ON
	    
      HEATER_ON
	    BSF PORTA, 2;
	    GOTO SICAKLIK_KONTROL_SONU;
	    
      HEATER_OFF
	    BCF PORTA, 2;


SICAKLIK_KONTROL_SONU
	    
INTERRUPT_SONU				;INTERRUPT PROGRAMI BURAYA KADAR YAZILIR


	    BCF INTCON, INTF;					;KESME BAYRAGI TEMIZLENIR KI YENIDEN KESME GEDIGINDE KESME ÇALISABILSIN
	    BCF INTCON, RBIF;					;KESME BAYRAGI TEMIZLENIR KI YENIDEN KESME GEDIGINDE KESME ÇALISABILSIN
	    BCF INTCON, T0IF;

	   


POP										;BURADAN ITIBAREN INTERRUPT ÖNCESI SAHIP OLUNAN DEGELERI GERI YÜKLENIR
	    ;MOVF PORTA_STATUS, W;			;PORTA VE PORTB DEGERLERINI GERI YÜKLEMEMIZ PROGRAMI BOZACAKTIR 
	    ;MOVWF PORTA;					;
	 
	    ;MOVF PORTB_STATUS, W;
	    ;MOVWF PORTB;
	    
	    MOVF DURUM_NO_TEMP, W;
	    MOVWF DURUM_NO;
	 
	    SWAPF STATUS_TEMP, W;
	    MOVWF STATUS;

	    SWAPF W_TEMP, F;
	    SWAPF W_TEMP, W;
	    BTFSC STATUS_TEMP, 6;
	    BSF STATUS, 2;
	    
	    CALL LEDLERI_YAK;
	  
	    RETFIE;
	    
;=====================================
;INTERRUPT PROGRAMI SONU
;=====================================

    
	    
	    
EKONOMIK_PROG
	    NOP;
	    RETURN;

HIZLI_PRO;
	    NOP;
	    RETURN;
	    
COK_KIRLI_PROGRAM
	    NOP;
	    RETURN;

NORMAL_PROG

	    ;ADIM_1 BASLANGIÇ
	    MOVF NORMAL_YIKAMA_ADIM1_SAYKIL_SAYISI, W;
	    MOVWF COUNT_1;
	    
	    CLRF DURUM_NO;						DURUM_NO = B'00' = YIKAMA.
	    CALL LEDLERI_YAK;
	    
LOOP_1
	    CALL BOSTA_BEKLE;
	    CALL KAZAN_SAGA_DONDUR_5SN;
	    CALL BOSTA_BEKLE;
	    CALL KAZAN_SOLA_DONDUR_5SN;

	    DECFSZ COUNT_1;
	    GOTO LOOP_1;
	    
	    CALL BOSTA_BEKLE;
	    
	    
	    CALL SUYU_BOSALT;
	    
	    CALL SIKMA;
	    
	    CLRF DURUM_NO;						DURUM_NO = B'00' = YIKAMA
	    CALL LEDLERI_YAK;
	    
	    ;ADIM_2 BASLANGIÇ	    
	    CALL SU_AL
	    
	    MOVF NORMAL_YIKAMA_ADIM2_SAYKIL_SAYISI, W;
	    MOVWF COUNT_1;
	    
LOOP_2
	    CALL BOSTA_BEKLE
	    CALL KAZAN_SAGA_DONDUR_5SN;
	    CALL BOSTA_BEKLE
	    CALL KAZAN_SOLA_DONDUR_5SN;
	    
	    DECFSZ COUNT_1;
	    GOTO LOOP_2
	    
	    CALL BOSTA_BEKLE;
	    
	    CALL SUYU_BOSALT;
	    
	    CALL SIKMA;
	    
	    CLRF DURUM_NO;							DURUM_NO B'0 0' YAPILARAK YIKAMA OLARAK AYARLANDI.
	    CALL LEDLERI_YAK
	    
	    ;ADIM_3 BASLANGIÇ	    
	    CALL SU_AL;
	    
	    MOVF NORMAL_YIKAMA_ADIM3_SAYKIL_SAYISI, W;
	    MOVWF COUNT_1;
	    
LOOP_3
	    CALL BOSTA_BEKLE;
	    CALL KAZAN_SAGA_DONDUR_5SN;
	    
	    CALL BOSTA_BEKLE;
	    CALL KAZAN_SOLA_DONDUR_5SN;
	    	    
	    DECFSZ COUNT_1;
	    GOTO LOOP_3
	    
	    CALL BOSTA_BEKLE;
	    
	    CALL SUYU_BOSALT;
	    
	    CALL SIKMA;
	    CALL SIKMA;
	     
	    
	    RETURN;					;NORMAL YIKAMA PROGRAMININ BITISI    
	    
KAZAN_SAGA_DONDUR_5SN
	    MOVLW B'1';
	    MOVWF SAG_SOL_FLAG;
	    MOVLW D'36';
	    MOVWF PWM_COUNT3;
	    MOVWF PWM_COUNT4;
	    CALL PWM_X_SN			;COUNT3*COUNT4 = 36*36 = 1296 VEDE BIR SAYKIL PWM_X_SN= 7.5 MILISANIYE OLDUGUNDAN 
								; PWM_X_SN ALT PROGRAMI 1296*7.5/1000 = 9.7 SANIYE KARE DALGA ÜRETIR.
	    RETURN;
	    
KAZAN_SOLA_DONDUR_5SN
	    CLRF SAG_SOL_FLAG;
	    MOVLW D'36';
	    MOVWF PWM_COUNT3;
	    MOVWF PWM_COUNT4;
	    CALL PWM_X_SN;
	    
	    RETURN;
	    
SUYU_BOSALT
	    MOVLW B'10';						DURUM_NO = B'10' = SIKMAYI TEMSIL ETMEKTEDIR;
	    MOVWF DURUM_NO;
	    
	    BCF PORTB, 3;						VANA KAPATILARAK SIKMA ESNASINDA KAZANIN SU ALMASI ÖNLENIR
	    
	    BCF PORTA, 1;						SU TAHLIYE TULUMBASI DEVREYE ALINIR
	    CALL BOSTA_BEKLE;				SU TAHIYE TULUMBASI BOSTA_BEKLE FONKSIONU KADAR ÇALISIR
	    CALL BOSTA_BEKLE;
	    BSF PORTA, 1;						SU TAHLIYE TULUMBASI DEVREDEN ÇIKARTILIR
	    RETURN;
	    
SIKMA
	    MOVLW B'10';						DURUM_NO = B'10' = SIKMAYI TEMSIL ETMEKTEDIR;
	    MOVWF DURUM_NO;
	    
	    BCF PORTA, 1;						SU TAHLIYE TULUMBASI DEVREYE ALINIR
	    BCF PORTB, 5;						KAZAN MOTORU DEVREYE ALINIR - KARE DALGASIZ TAM PERFORMANS DÖNEREK SIKMA YAPAR
	    
	    CALL BOSTA_BEKLE;				SIKMA SÜRESININ BELIRLENMESI
	    CALL BOSTA_BEKLE;
	    CALL BOSTA_BEKLE;
	    
	    BSF PORTB, 5;						SU TAHLIYE VE KAZAN MOTORU DEVREDEN ÇIKARTILIR;
	    BSF PORTA, 1;
	    RETURN;

	    
SU_AL
	    BTFSC PORTB, 4;
	    GOTO OFF_VANA;
	    
	    MOVF DURUM_NO, W
	    SUBLW B'00';
	    BTFSS STATUS, Z;
	    GOTO  OFF_VANA;
	    GOTO ON_VANA;
	    
ON_VANA	    
	    BSF PORTB, 3;
	    RETURN;
	    
OFF_VANA
	    BCF PORTB, 3;
	    RETURN;
;SU_AL  ALT PROGRAMININ SONU




;=================================================================================================
;PULS WIDTH MODULATION ALT PROGRAMI
;=================================================================================================	 
   
PWM_X_SN					;BU ALT PROGRAM KAZAN MOTORUNUN YAVAS DEVIRDE DÖNMESI 
							;IÇIN GEREKLI OLAN KARE DALGAYI ÜRETIR KAÇ SANIYE BOYUNCA ÜRETILECEGI
							;SAGA_SOLA_DONME_SURESI REGISTERINDAN ALINMAKTADIR.

PWM_MAIN
	    MOVLW D'250';
	    MOVWF CYCLE;	CYCLE = 250
	    
COUNT3_YUKLE
	    MOVF SAGA_SOLA_DONME_SURESI, W;
	    MOVWF PWM_COUNT3;
PWM
	    MOVLW D'50';
	    MOVWF PON;		PON = 50
	    
	    SUBWF CYCLE, W;
	    MOVWF POFF;		POFF = 200
	    
	    BTFSS SAG_SOL_FLAG, 0;
	    BCF PORTB, 6;
	    BTFSC SAG_SOL_FLAG, 0;
	    BCF PORTB, 5;
	    
PWM0
	    CALL TIME1; 	YAKLASIK 0.03 MILISANIYE GECIKTIRME SAGLAR
				;	BU ALT PROGRAM 50 DEFA ÇAGRILARAK 
				;	50*0.03 =1.5 MILISANIYE 0V ÇIKIS VERILMIS OLUR
				
	    DECFSZ PON, F;
	    GOTO PWM0;
	    
	    BSF PORTB, 6;
	    BSF PORTB, 5;
	    
PWM1
	    CALL TIME1;	YAKLASIK 0.03 MILISANIYE GECIKTIRME SAGLAR
				;	BU ALT PROGRAM 200 DEFA ÇAGRILARAK 
				;	200*0.03 =6 MILISANIYE +5V ÇIKIS VERILMIS OLUR
				
	    DECFSZ POFF, F;
	    GOTO PWM1;
	    
	    DECFSZ PWM_COUNT3;
	    GOTO PWM;
	    DECFSZ PWM_COUNT4;
	    GOTO COUNT3_YUKLE
	    RETURN;

TIME1
	    MOVLW D'1';				1 CLOCK
	    MOVWF PWM_COUNT1;		1 CLOCK
	   
DELAY1
	    MOVLW D'2';				1 CLOCK
	    MOVWF PWM_COUNT2;		1 CLOCK
	    
DELAY2
	    GOTO $+1;					2 CLOCK
	    GOTO $+1;					2 CLOCK
	    GOTO $+1;					2 CLOCK
	    NOP;						1 CLOCK
	    DECFSZ PWM_COUNT2, F;		1(2) CLOCK
	    GOTO DELAY2;				2 CLOCK
	    DECFSZ PWM_COUNT1, F;		1(2) CLOCK
	    GOTO DELAY1;				2 CLOCK
	    RETURN;					2 CLOCK
	    
;PWM_X_SN ALT PROGRAMI SONU


BOSTA_BEKLE
	    MOVF BOSTA_BEKLEME_SURESI, W;
	    MOVWF COUNT_2;
	    MOVLW D'255';
	    
	    
BOS_LOOP1	    
	    MOVWF COUNT_3;
	    
BOS_LOOP2
	    MOVWF COUNT_4;
	    
BOS_LOOP3
	    
	    GOTO $+1;
	    GOTO $+1;
	    GOTO $+1;
	    GOTO $+1;
	    GOTO $+1;
	    GOTO $+1;
	    	    
	    DECFSZ COUNT_4;
	    GOTO BOS_LOOP3
	   
	    DECFSZ COUNT_3;
	    GOTO BOS_LOOP2;
	    
	    DECFSZ COUNT_2;
	    GOTO BOS_LOOP1;
	    
	    RETURN;
	    
	    
LEDLERI_YAK
	    BTFSC DURUM_NO, 0;
	    GOTO SET_PORT_B1;
	    GOTO CLEAR_PORT_B1
	    
SET_PORT_B1
	    BSF PORTB, 1;
	    GOTO SONRAKI_LED

CLEAR_PORT_B1
	    BCF PORTB, 1;
	    GOTO SONRAKI_LED;
	     
SONRAKI_LED
	    BTFSC DURUM_NO, 1;
	    GOTO SET_PORT_B2;
	    GOTO CLEAR_PORT_B2

SET_PORT_B2
	    BSF PORTB, 2;
	    RETURN;
	    
CLEAR_PORT_B2
	    BCF PORTB, 2;
	    RETURN;
;LEDLERI_YAK ALT PROGRAMININ SONU
	    

;====================================================================
      END
